buildscript {
    apply from: 'gradle/dependencies/versions.gradle'

    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.gradle}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    afterEvaluate {
        if (!project.hasProperty("android")) return

        android {
            compileSdkVersion application.compileSdk
            buildToolsVersion application.buildTools

            defaultConfig {
                minSdkVersion application.minSdk
                targetSdkVersion application.targetSdk
                versionCode application.versionCode
                versionName application.versionName

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                consumerProguardFiles "consumer-rules.pro"
            }

            testOptions {
                animationsDisabled = true
            }

            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            kotlinOptions {
                jvmTarget = JavaVersion.VERSION_1_8.toString()
            }

            sourceSets.forEach { sourceSet ->
                sourceSet.jniLibs.srcDirs = ['libs', "libs/${sourceSet.name}"]
                sourceSet.java.srcDirs += "src/${sourceSet.name}/kotlin"
                sourceSet.assets.srcDirs += "src/${sourceSet.name}/assets"
            }

            packagingOptions {
                exclude 'META-INF/DEPENDENCIES'
                exclude 'META-INF/LICENSE'
                exclude 'META-INF/LICENSE.txt'
                exclude 'META-INF/license.txt'
                exclude 'META-INF/NOTICE'
                exclude 'META-INF/NOTICE.txt'
                exclude 'META-INF/notice.txt'
                exclude 'META-INF/ASL2.0'
                exclude('META-INF/*.kotlin_module')
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}